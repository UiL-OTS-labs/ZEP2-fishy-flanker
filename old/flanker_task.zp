/******************************************************************************\
FILE:           flanker_task.zp
AUTHOR:         Chris van Run <C.P.A.vanRun@uu.nl>
ADAPTED BY:     Maarten

DESCRIPTION:

In this task there are 5 arrows. The one in the middle is flanked by 2 arrows
on both sides. Participant respond with a left and right button, depending on
the direction of the arrow in the center. If the flanking arrows point in the
same direction as the center arrow it is a congruent trial, if the flankers
point in the opposite direction the trial type is called incongruent.
Additionally, there are trials flanked by stripes, these are the Neutral trials
because the stripes do not point in a direction.

There is a practice round.

Total trial time is now set at ~2 seconds so there is a delay after the
response that is dependent on the response, ISI, fixation_cross_duration
If response is longer than the length duration it defaults to 0s.

Randomization is according to these rules:
No more then 3 consecutive sides (direction).
No more then 2 consecutive flanker (congruency).
Never two consecutive trials that have the same congruency AND side.

HISTORY:
2014-11-03 CvR   Created.

\******************************************************************************/

requires 2.6;

//globally used modules
import std_windows1;
import std_entry_page1;
import std_exit_page1;
import std_blank_page;
import std_pause_page;

import texts_cn;

import io_beexybox;

import test::task;
import test::shuffle;

const int EXPERIMENT_VERSION = 2;

Experiment experiment
{

    on_event:entry()
    {
        show_test_windows();
        control.enable_all_buttons(CONTINUE);

        if (test::load_stimuli() != OK)
            terminate;
    }

    on_event:exit()
    {
        terminate;
    }


    on_event:message()
    {
        // On Ctrl+F4 jump unconditionally to the thank-you part.
        if (message_sender == control && message_arg == SYS_STOP)
        {
            control.mark_experiment_aborted();
            thankyou.enter();
        }
    }

    Part welcome
    {
        on_event:entry()
        {
            entry_page.set_text(WELCOME_TEXT);
            entry_page.action(this, 0s);
        }
    }

	Part
    {
        on_event:entry()
        {
            blank_page.fill_pattern_color = TEST_PAGE_COLOR;
            blank_page.action_auto(this, 500ms);
        }
    }

    Part main_experiment
    {
        on_event:entry()
        {
            test::action(this);
        }
    }

    Part
    {
        on_event:entry()
        {
            blank_page.fill_pattern_color = TEST_PAGE_COLOR;
            blank_page.action_auto(this, 500ms);
        }
    }

    Part thankyou
    {
        on_event:entry()
        {
            control.end_experiment();   // Check-in session.

            exit_page.set_text(THANK_YOU_TEXT);
            exit_page.action(this, 10s);
        }
    }
}
